$PROBLEM    Simulated BE- Passes
$INPUT      ID TIME AMT  RATE  CMT CMTT DV WT EGFR AGE COV1 COV2 EVID OCC GROUP TRT SEQ BLQ
 
$DATA C:/git/mbbe/inst/examples/DATA.csv IGNORE=@ REWIND
$OMEGA  
  1  FIX  	;  ETA(1) CL
  1  FIX  	;  ETA(2) V2 
  1  FIX  	;  ETA(3) KA REFERENCE
  1  FIX  	;  ETA(4) KA TEST
  1  FIX  	;  ETA(5) ALAG1 REFERENCE
  1  FIX 	;  ETA(6) ALAG1 TEST 
  1  FIX  	;  ETA(7) D1 REFERENCE
  1  FIX 	;  ETA(8) D1 TEST 
$OMEGA  BLOCK(4)
  1  FIX ; ETA(9) BOVV
  0 1    ; ETA(10) BOVCL
  0 0 1    ; ETA(11) BOVKA
  0 0 0 1    ; ETA(12) BOVALAG1
$OMEGA   BLOCK(4) SAME ; ETAs 13,14,15,16
$OMEGA   BLOCK(4) SAME ; ETAs 17,18,19,20
$OMEGA   BLOCK(4) SAME ; ETAs 21,22,23,24
$SIGMA  
  1  FIX  ; EPS(1) PROPORTIONAL
  1  FIX  ; EPS(2) ADDITIVE
  ;;;; Start subs
$SUBROUTINE ADVAN4 ;; advan4  
$PK
  CWTKG	= WT/70
  CAGE	= AGE/24
  CEGFR	= EGFR/100
  CCOV1	= COV1/10
  CCOV2 = COV2/10 


  IF(GROUP.EQ.1) BOVV = THETA(18)*ETA(9)
  IF(GROUP.EQ.2) BOVV = THETA(18)*ETA(13)
  IF(GROUP.EQ.3) BOVV = THETA(18)*ETA(17)
  IF(GROUP.EQ.4) BOVV = THETA(18)*ETA(21)

  IF(GROUP.EQ.1) BOVCL = THETA(19)*ETA(10)
  IF(GROUP.EQ.2) BOVCL = THETA(19)*ETA(14)
  IF(GROUP.EQ.3) BOVCL = THETA(19)*ETA(18)
  IF(GROUP.EQ.4) BOVCL = THETA(19)*ETA(22)

  IF(GROUP.EQ.1) BOVKA = THETA(20)*ETA(11)
  IF(GROUP.EQ.2) BOVKA = THETA(20)*ETA(15)
  IF(GROUP.EQ.3) BOVKA = THETA(20)*ETA(19)
  IF(GROUP.EQ.4) BOVKA = THETA(20)*ETA(23)

  BOVALAG1=0  
  ;; COVARIATE POWER MODEL WITH COVARIATE CENTERED AT 1 CAN BE +IVE OR -IVE
  TVCL	= EXP(THETA(1)) *CWTKG**THETA(21)    
  CL 	= TVCL*EXP(THETA(3)*ETA(1))*EXP(BOVCL)  ;; ETACLV IS CORRELATION OF ETA(V) AND ETA(CL)
  TVV	= EXP(THETA(2))    
  V2 	= TVV*EXP(THETA(4)*ETA(2))*EXP(BOVV)  
  IF(TRT.EQ.1) THEN ;; REFERENCE
  KA 	= EXP(THETA(5) + BOVKA  +THETA(16)*ETA(3))  
  F1	= 1
  ALAG1	= EXP(THETA(12))
  D1	= EXP(THETA(14))
  ELSE   ;; TEST
  KA 	= EXP(THETA(6) + BOVKA +THETA(17)*ETA(4))  
  F1 	= EXP(THETA(7))
  ALAG1	= EXP(THETA(13))
  D1	= EXP(THETA(15))
  END IF   
  S2   	= V2/1000 	; CONC IN NG/ML (MCG/L), DOSE IN MG, VOL IN L 
  K=CL/V2  

  K32 	= EXP(THETA(10))
  K23	= EXP(THETA(11))

$ERROR  
  LLOQ 		= 0.01 
  IPRED 	= F  
  REALOBS 	= DV ;;; TO PRINT OUT FOR ORG.DAT
  ADD  		= EXP(THETA(8)) ;; NEEDS TO BE HERE, TO EDIT IN SPPC
  PROP 		= EXP(THETA(9))
  SD 		= SQRT(PROP**2*IPRED**2 + ADD**2) ; Residual weight ADD AND P PROP IN SD AND CV UNITS, NOT VARIANCE
  OBSWERROR	= IPRED + SD*EPS(1) ;; observation with error

  IF(BLQ.EQ.0) THEN
  F_FLAG = 0
  Y 	= IPRED + SD*EPS(1)                       
  ELSE
  F_FLAG = 1
  Y	= PHI((LLOQ-IPRED)/SD)
  END IF

  ;;;; Start EST


$EST METHOD=COND INTER LAPLACE SADDLE_RESET=1 MAX=9999 NOABORT PRINT = 50 

$THETA ;; all are log of THETA
  (-1,5,10)  		; THETA(1) LN(CL)
  (-1,4,10)  		; THETA(2) LN(V2) 
  (-4,-0.61) 		; THETA(3) LN(ETA(CL))
  (-4,-0.38)		; THETA(4) LN(ETA(V))
  (-5,-0.4,5)	 	; THETA(5) LN(KA) REFERENCE
  (-5,0.6,5)	 	; THETA(6) LN(KA) TEST 
  (-3,0.15,1)	 	; THETA(7) LN(F) TEST  
  (-4,1)		  	; THETA(8) LN(ADD ERROR)
  (-4,-1.5)	  	; THETA(9) LN(PROP ERROR) 

  (-5,-2.53064,5) 		; THETA(10) LN(K32)
  (-5,-0.89632,5) 		; THETA(11) LN(K23)
  (-5,-2,5)		; THETA(12) LN(ALAG) REFERENCE 
  (-5,-2,5) 		; THETA(13) LN(ALAG) test
  (-7,-2.58,5) 		; THETA(14) LN(D1) REFERENCE
  (-7,-2.58,5)		 ; THETA(15) LN(D1) TEST
  (-4,-1,4) 		; THETA(16) KA ETA REFERENCE
  (-4,-1,4) 		 ; THETA(17) KAETA TEST
  (-4,-0.3,5)	; THETA(18) BOVV 
  (-4,-0.3,5)	; THETA(19) BOVCL 
  (-4,-0.3,5)	; THETA(20) BOVKA 
  ;; NO BOVALAG1  
  ;; NO COVARIANCE BETWEEN V AND CL  
  ;; covariate parameters need not be log transformed, they can be < 0

  0.7		; THETA(21) CL~WT 

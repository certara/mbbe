$PROBLEM    true model BE- Passes
$INPUT      ID TIME AMT RATE CMT CMTT DV WT EGFR AGE COV1 COV2 EVID PERIOD GROUP TRT SEQ
  
$DATA C:/FDA/SIMULATED/BE-PASSES/DATA.csv IGNORE=@
$OMEGA  
  1  FIX  	;  ETA(1) CL
  1  FIX  	;  ETA(2) V2 
  1  FIX  	;  ETA(3) D1
$OMEGA  BLOCK(1) 
  1    	; ETA(4) BOVCL  
$OMEGA   BLOCK(1) SAME ; ETAs 5
$OMEGA   BLOCK(1) SAME ; ETAs 6
$OMEGA   BLOCK(1) SAME ; ETAs 7
$SIGMA  
  1  FIX  ; EPS(1) PROPORTIONAL
  1  FIX  ; EPS(2) ADDITIVE
  ;;;; Start subs
$SUBROUTINE ADVAN4 ;; advan4
$PK
  CWT 	= WT/70
  CAGE	= AGE/24
  CEGFR	= EGFR/100
  CCOV1	= COV1/10
  CCOV2 = COV2/10 
  IF(PERIOD.EQ.1) BOVCL = THETA(12)*ETA(4)
  IF(PERIOD.EQ.2) BOVCL = THETA(12)*ETA(5)
  IF(PERIOD.EQ.3) BOVCL = THETA(12)*ETA(6)
  IF(PERIOD.EQ.4) BOVCL = THETA(12)*ETA(7)
  CL	= EXP(THETA(1)+THETA(8)*ETA(1)+ BOVCL)*CWT**THETA(16)*CEGFR**THETA(18)  
  V2 	= EXP(THETA(2)+THETA(9)*ETA(2))*CWT**THETA(17) 
  ALAG1	= EXP(THETA(5))  
  KA 	= EXP(THETA(6)) 
  D1	= EXP(THETA(13)+THETA(14)*ETA(3))  
  IF(TRT.EQ.1) THEN ;; REFERENCE 
     F1	= 1
     KA 	= EXP(THETA(6)) 
  ELSE   ;; TEST 
     F1 = EXP(THETA(7))
     KA 	= EXP(THETA(15)) 
  END IF   

  S2   	= V2/1000 	; CONC IN NG/ML (MCG/L), DOSE IN MG, VOL IN L 
  K=CL/V2  
 
  K32 = EXP(THETA(10))
  K23 = EXP(THETA(11))
  REP = IREP
$ERROR   
  IPRED = F  
  REALOBS = DV ;;; TO PRINT OUT FOR ORG.DAT
  ADD 	= EXP(THETA(3))
  PROP 	= EXP(THETA(4)) 
  SD 	= SQRT(PROP**2*IPRED**2 + ADD**2) ; Residual weight ADD AND P PROP IN SD AND CV UNITS, NOT VARIANCE
  OBSWERROR= IPRED + SD*EPS(1) ;; observation with error
  Y 	= IPRED + SD*EPS(1)                       

  ;;;; Start EST
;$SIM ONLYSIM ( 9786 )
 
$EST METHOD=COND INTER SADDLE_RESET=1 MAX=9999 NOABORT PRINT = 10 MSFO = msf1
$COV PRINT=E UNCOND PRECOND=1

$THETA ;; all are log of THETA
5  	;; THETA(1) LN(CL)
4  	;; THETA(2) LN(V2) 
1  	;; THETA(3) ADD ERROR
-1.5  	;; THETA(4) PROP ERROR
-0.5	;; THETA(5) LN(ALAG)
-0.7  	;; THETA(6) LN(KA) referene
-0.03 	;; THETA(7) LN(F1) TEST 
-1  	;; THETA(8) LN(BSV) CL
-1  	;; THETA(9) LN(BSV) V2 
-0.8	;; THETA(10) LN(K32)
-0.4	;; THETA(11) LN(K23)
-1.5  	;; THETA(12) LN(BOV) CL 
-0.8	;; THETA(13) LN(D1)
-1.5	;; THETA(14) BSVD1
0.1  	;; THETA(15) LN(KA) test
;; covariate parameters need not be log transformed, they can be < 0
0.75	;; THETA(16) CL~WT CENTERED ON 1
1	;; THETA(17) V~WT CENTERED ON 1
0.1	;; THETA(18) CL~EGFR CENTERED ON 1 
$TABLE  ID   TIME DV AMT MDV NOAPPEND NOPRINT FILE=OBS.DAT NOHEADER

$PROB simulation
$INPUT      ID TIME AMT RATE CMT CMTT DV WT EGFR AGE COV1 COV2 EVID PERIOD GROUP TRT SEQ
  
$DATA C:/FDA/SIMULATED/BE-PASSES/DATA.csv IGNORE=@ REWIND
$MSFI = msf1
$SIM ONLYSIM NSUB = 50 (2345)
$TABLE    ID REP  TIME  DV  IPRED  PRED AMT MDV NOAPPEND NOPRINT FILE=SIM.DAT NOHEADER
 
